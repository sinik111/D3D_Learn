#include "Shared.hlsli"

Texture2D g_gBufferBaseColor : register(t0);
Texture2D g_gBufferPosition : register(t1);
Texture2D g_gBufferNormal : register(t2);
Texture2D g_gBufferEmissive : register(t3);
Texture2D g_gBufferORM : register(t4);

Texture2D g_texShadowMap : register(t8);
TextureCube g_texIblIrradiance : register(t9);
TextureCube g_texIblSpecular : register(t10);
Texture2D g_texIBLSpecularBrdfLut : register(t11);

SamplerState g_samClamp : register(s2);

static const float3 DielectricFactor = 0.04f;

float NDFGGXTR(float nDotH, float roughness)
{
    float a = roughness * roughness;
    float aSq = a * a;
    
    float denom = (nDotH * nDotH) * (aSq - 1.0f) + 1.0f;
    
    return aSq / (PI * denom * denom);
}

float3 FresnelSchlick(float3 f0, float cosTheta)
{
    return f0 + (1.0f - f0) * pow(1.0f - cosTheta, 5.0f);
}

float GAFSchlickGGXSub(float cosTheta, float k)
{
    return cosTheta / (cosTheta * (1.0f - k) + k);
}

float GAFSchlickGGX(float nDotV, float nDotL, float roughness)
{
    float a = roughness + 1.0f;
    float k = (a * a) / 8.0f;
    
    return GAFSchlickGGXSub(nDotV, k) * GAFSchlickGGXSub(nDotL, k);
}

float4 main(PS_INPUT_QUAD input) : SV_Target
{
    float3 texDiffColor = g_gBufferBaseColor.Sample(g_samLinear, input.tex).rgb;
    float3 texNormColor = g_gBufferNormal.Sample(g_samLinear, input.tex).rgb;
    float3 worldPos = g_gBufferPosition.Sample(g_samLinear, input.tex).rgb;
    float3 texEmsvColor = g_gBufferEmissive.Sample(g_samLinear, input.tex).rgb;
    float3 orm = g_gBufferORM.Sample(g_samLinear, input.tex).rgb;
    float ambientOcclusionFactor = orm.r;
    float roughnessFactor = orm.g;
    float metalnessFactor = orm.b;
        
    if (g_overrideMaterial)
    {
        texDiffColor = (float3) g_overrideBaseColor;
        metalnessFactor = g_overrideMetalness;
        roughnessFactor = g_overrideRoughness;
    }
    
    roughnessFactor = max(EPSILON, roughnessFactor);
    
    // normal
    float3 n = DecodeNormal(texNormColor);
    
    // view
    float3 v = normalize(g_cameraPos - worldPos);
    
    // light
    float3 l = -g_lightDir;
    
    // l-v half
    float3 h = normalize(l + v);
    
    float nDotH = max(0.0f, dot(n, h));
    
    float nDotL = max(0.0f, dot(n, l));
    
    float nDotV = max(0.0f, dot(n, v));
    
    float hDotV = max(0.0f, dot(h, v));
    
    // shadow
    float4 lightClipPos = mul(float4(worldPos, 1.0f), g_lightViewProjection);
    
    float shadowFactor = 1.0f;
    float currentShadowDepth = lightClipPos.z / lightClipPos.w;
    float2 shadowMapUV = lightClipPos.xy / lightClipPos.w;
    
    shadowMapUV.y = -shadowMapUV.y;
    shadowMapUV = shadowMapUV * 0.5f + 0.5f;
    
    if (all(shadowMapUV >= 0.0f) && all(shadowMapUV <= 1.0f))
    {
        if (g_useShadowPCF)
        {
            if (currentShadowDepth > 1.0f)
            {
                shadowFactor = 1.0f;
            }
            else
            {
                float texelSize = 1.0f / g_shadowMapSize;

                int max = g_pcfSize;
                float sum = 0.0f;
                for (int y = -max; y <= max; ++y)
                {
                    for (int x = -max; x <= max; ++x)
                    {
                        float2 offset = float2(x, y) * texelSize;
                        float2 sampleUV = shadowMapUV + offset;

                        sum += g_texShadowMap.SampleCmpLevelZero(g_samComparison, sampleUV, currentShadowDepth - 0.0001f);
                    }
                }
                shadowFactor = sum / ((max * 2 + 1) * (max * 2 + 1));
            }
        }
        else
        {
            float sampleShadowDepth = g_texShadowMap.Sample(g_samLinear, shadowMapUV).r;
            if (currentShadowDepth > 1.0f)
            {
                shadowFactor = 1.0f;
            }
            else if (currentShadowDepth > sampleShadowDepth + 0.001f)
            {
                shadowFactor = 0.0f;
            }
        }
    }
    
    float3 f0 = lerp(DielectricFactor, texDiffColor, metalnessFactor);
    
    float g = GAFSchlickGGX(nDotV, nDotL, roughnessFactor);
    
    float3 directLighting = 0.0f;
    {
        float d = NDFGGXTR(nDotH, max(0.1f, roughnessFactor));
        
        float3 f = FresnelSchlick(f0, hDotV);
        
        float3 kd = lerp(1.0f - f, 0.0f, metalnessFactor);
        
        float3 diffuseBRDF = kd * texDiffColor / PI;
        float3 specularBRDF = (f * d * g) / max(EPSILON, 4.0f * nDotL * nDotV);
    
        directLighting = (diffuseBRDF + specularBRDF) * (float3) g_lightColor * g_lightIntensity * nDotL * shadowFactor;
    }
    
    float3 ambientLighting = 0.0f;
    if (g_useIBL)
    {
        float3 f = FresnelSchlick(f0, nDotV);
        
        float3 kd = lerp(1.0f - f, 0.0f, metalnessFactor);
        
        float3 irradiance = g_texIblIrradiance.Sample(g_samLinear, n).rgb;
    
        float3 diffuseIBL = kd * texDiffColor / PI * irradiance;
    
        uint specularTextureLevels, width, height;
        g_texIblSpecular.GetDimensions(0, width, height, specularTextureLevels);
    
        float3 viewReflect = -(v - 2.0 * nDotV * n);
    
        float3 prefilteredColor = g_texIblSpecular.SampleLevel(g_samLinear, viewReflect, roughnessFactor * specularTextureLevels).rgb;
    
        float2 specularBRDF = g_texIBLSpecularBrdfLut.Sample(g_samClamp, float2(nDotV, roughnessFactor)).rg;
    
        float3 specularIBL = prefilteredColor * (f0 * specularBRDF.x + specularBRDF.y);
        
        ambientLighting = (diffuseIBL + specularIBL) * (ambientOcclusionFactor * g_ambientOcclusion);
    }
    
    float3 final = directLighting + ambientLighting + texEmsvColor;
    
    final = max(0.0f, final);
    
    //return float4(pow(final, 1.0f / 2.2f), 1.0f);
    return float4(final, 1.0f);
}